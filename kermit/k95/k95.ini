; K95.INI - Standard Kermit 95 initialization file.
;
; V3.0, May 2000
;
; This file sets up the standard and expected environment for Kermit 95.
; Please don't edit this file.  Make any desired customizations in the
; K95CUSTOM.INI file, which is executed by this file.

echo Executing \v(cmdfile) for \v(system)...
if < \v(xversion) 1121 forward custom  ; K95 too old

if iks forward custom

; Dialing and Network directories

; set dial directory ckermit.kdd cis.kdd sprint.kdd tymnet.kdd genie.kdd
; set network directory ckermit.knd

; Editor - Version 1.1.9 or later required

if def \v(editor) forward netedit     ; Picked up from registry?

; Set up the default editor for the EDIT command.  If you would rather use
; a different editor, then either define an EDITOR environment variable, or
; add a SET EDITOR command to your K95CUSTOM.INI file to override this one.
;
xif not def \$(EDITOR) {
    xif def \$(SystemRoot) {                    ; Windows NT
        set editor \$(SystemRoot)\\NOTEPAD.EXE
    } else {
        xif def \$(windir) {                    ; Windows 95
            set editor \$(windir)\\NOTEPAD.EXE
        } else {
            echo ERROR setting default editor name --
            echo Unable to locate proper Windows Environment variables  
        }
    }
}

:NETEDIT

; NETEDIT macro -- See UPDATES.TXT for an explanation.
;  
def _NETEDIT {
    switch \v(protocol) {
      :kermit,   output kermit -s \%1\13, break
      :zmodem,   output sz -a \%1\13, break
      :ymodem,   output sb -a \%1\13, break
      :ymodem-g, output sb -a \%1\13, break
      :xmodem,   output sx -a \%1\13, break
      :default, end 1 "\v(protocol)" - protocol unexpected
    }
    receive \%1
    if fail end 1 "\%1" - Download failed
    edit \%1
    if def WAIT_FOR_EDITOR getok
    if not exist \%1 end 1 -
      File "\%1" missing after edit - Did you change its name\?
    set file type text
    send \%1
    asg status \v(status)
    delete \%1
    if not = \m(status) 0 end 1 Upload failed
    end 0
}

def NETEDIT {
    local mode current
    asg mode \v(ftype)			; Save things
    set term autodownload off
    asg current \v(directory)
    if def \v(download) cd \v(download)
    do _netedit \%1			; Execute the macro
    asg status \v(status)
    set term autodownload on		; Restore things
    cd \m(current)
    set file type \m(mode)
    end \m(status)			; Return
}

; The K95CUSTOM.INI file is created for you by SETUP.
; You can edit K95CUSTOM.INI to suit your preferences.

:CUSTOM

if exist \$(K95CUSTOM) -
  take \$(K95CUSTOM)
else if exist \v(startup)K95CUSTOM.INI -
  take \v(startup)K95CUSTOM.INI 
else if exist \v(inidir)K95CUSTOM.INI -
  take \v(inidir)K95CUSTOM.INI 
else if exist \v(exedir)K95CUSTOM.INI -
  take \v(exedir)K95CUSTOM.INI 

if < \v(ntime) 43200 echo Good Morning, smile!
  else if < \v(ntime) 61200 echo Good Afternoon, smile!
  else echo Good Evening, smile!

end ; of K95.INI
